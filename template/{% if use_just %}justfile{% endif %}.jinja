# Default recipe to display help
_default:
    @just --list

# Build a python wheel package
build:
    uv build --wheel

# Bump the project version and create a tag
bump VERSION_PART="":
    #!/usr/bin/env bash
    if [ -z "{{VERSION_PART}}" ]; then
        read -p "Version part [major, minor, patch]: " version_part
    else
        version_part="{{VERSION_PART}}"
    fi
    uv run bump-my-version bump $version_part

# Setup the development environment
setup:
    uv sync
    cp .pre-commit-config.standard.yaml .pre-commit-config.yaml
    uv run pre-commit install

# Setup the development environment with strict pre-commit rules
setup-strict: setup
    @echo "Appending strict pre-commit rules..."
    cat .pre-commit-config.addon.strict.yaml >> .pre-commit-config.yaml

# Runs linting on all project files
lint:
    #!/usr/bin/env bash
    tempfile=$(mktemp)
    trap 'rm -f $tempfile' EXIT
    cat .pre-commit-config.standard.yaml .pre-commit-config.addon.strict.yaml > $tempfile
    uv run pre-commit run --all-files -c $tempfile

# Run the project tests
test:
    uv run pytest

{% if generate_docs == "mkdocs" %}
# Build documentation
docs:
    uv run mkdocs build --site-dir build/site

# Build documentation and serve it locally
serve-docs:
    uv run mkdocs serve
{% endif %}

{% if generate_docs == "pdoc" %}
# Build documentation
docs:
    uv run pdoc --output-directory build/site src/{{ package_name }}

# Build documentation and serve it locally
serve-docs:
    uv run pdoc src/{{ package_name }}
{% endif %}
